# 3.3 属性和索引属性

属性有和字段相同的语法，但是通过 getter、setter 运算符获取和修改值。它和 C#、JavaScript、Kotlin 等中的 getter、setter 作用一样。

## 属性

定义一个属性的 getter 和 setter 的语法如下：

```aoi
get self.proertyName: SelfType -> PropertyType {
    ...
}
```

```aoi
set self.propertyName = value: SelfType <- PropertyType {
    ...
}
```

其中，`propertyName` 是属性名，`self`、`value` 是两个声明模式，分别记录属性所在对象的值和被赋的值。`SelfType` 和 `PropertyType` 分别是属性所在对象和属性的类型。`{ ... }` 是 getter 或 setter 的主体块，在 getter 中是 `PropertyType` 类型的表达式块，而在 setter 中则是 `SelfType` 类型的，它需要返回赋值之后的 `self`。

同一个属性的 getter 和 setter 对应的类型必须一致。getter 和 setter 可以只定义一个。这时候，属性只读或只写。

属性作为右值时调用 getter，作为左值被赋值时调用 setter。在 C# 等语言中，值类型属性中的字段或属性作为左值赋值时会报错，而 Aoi 中为解决这种不便，会调用一次 getter，将得到的值的属性或字段进行修改，并调用一次 setter 将修改后的值赋给属性。即，对于 `a.prop.x = b`，其中 `prop` 是 `a` 的值类型属性，它等价于：

```aoi
{
    let temp = a.prop;
    temp.x = b;
    a.prop = temp
}
```

## 索引属性

索引属性也是属性，但是它从用一个标识符访问变为了用一个值作为索引访问。

定义一个索引属性的 getter 和 setter 的语法如下：

```aoi
get self.[index]: SelfType <> IndexType -> PropertyType {
    ...
}
```

```aoi
set self.[index] = value: SelfType <> IndexType <- PropertyType {
    ...
}
```

其中，`self`、`index`、`value` 是三个声明模式，分别记录属性所在对象的值、索引的值和被赋的值。`SelfType`、`IndexType` 和 `PropertyType` 分别是属性所在对象和属性的类型。`{ ... }` 是 getter 或 setter 的主体块。

例如，以下代码定义了一个简单的 `string` 到 `int` 的字典：

```aoi
class Dictionary {
    keyValuePairs: arr[(int, int)];
}
get self.length: Dictionary -> int {
    self.keyValuePairs.length
}
get self.[key]: Dictionary <> str -> int {
    for (k, v) in self.keyValuePairs do {
        if k == key then {
            return v;
        }
    }
    throw new KeyNotFoundException {}
}
set self.[key] = value: Dictionary <> str <- int {
    for i in 0..self.length do {
        if self.keyValuePairs.[i].0 == key then {
            self.keyValuePairs.[i].1 = value;
            return self;
        }
    }
    self
}
```
